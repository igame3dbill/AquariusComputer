new

40 H=0:W=1024:K=14350
50 GOSUB1000
90 G=G+5:J=J+1
100 FORA=65TO(G+65)
110 B=INT(876*RND(1)+12369):IFPEEK(B)<>32THEN110
120 POKEB,A:POKEB+W,113:SOUND(40,10):NEXTA:L=65:D=12369:POKED,197
150 POKEK,0:IFPEEK(K)=0THEN150
200 SOUND(Z,150):IFPEEK(K)=42THENY=-40
205 IFPEEK(K)=40THENY=40
210 IFPEEK(K)=12THENY=-1
215 IFPEEK(K)=6THENY=1
220 IFPEEK(D+Y+W)=113THENIFPEEK(D+Y)<>LTHEN300
225 IFPEEK(D+Y)=LTHENL=L+1:S=S+1:SOUND(180,20)
230 IFPEEK(D+Y)=135THENSOUND(3,325)
235 POKED,135:POKED+W,59:D=D+Y:POKED,197:POKED+W,54:IFL=G+66THEN400
240 GOTO200
300 FORB=1TO500STEP20:SOUND(15,B):NEXT:GOTO50
400 ?CHR$(11):FORA=1TO10:?:NEXT:?TAB(14)"Screen ";J:?
401 ?TAB(14)"Completed"
410 FORA=1TO10:FORB=130TO30STEP-10:SOUND(5,B):NEXT:NEXT
420 IFJ=4THENFORA=1TO7:?:?TAB(2*A)"YOU WIN"CHR$(7):NEXT:GOTO50
430 GOSUB1200:GOTO90
1000 ?CHR$(11):?TAB(4)"SNAKE   ";CHR$(5);" Digital Output 1984":?:?
1010 ?"   See packaging for instructions":?
1011 ?TAB(3)"It's okay to cross your own trail"
1012 ?TAB(9)"Go to letters in order"
1013 ?TAB(9)"Play using keys a z , .":?
1020 ?TAB(10)"Best  Score =";H:?TAB(10)"Last  Score =";S:?:?
1030 IFS>HTHEN?:?TAB(7)"This is a new high score !":H=S:?
1040 ?:?"What Level?  1 (easy) to 9 (hard)":S=0
1050 Z$=INKEY$:A=RND(1):IFZ$=""THEN1050
1060 Z=VAL(Z$):IFZ<1ORZ>9THEN1050
1100 Z=(10-Z)*4:G=5:J=0
1200 B=30:C=920:?CHR$(11)
1205 FORA=13312TO13351:POKEA,1:NEXT
1210 FORA=12328TO12367:GOSUB1250:NEXTA:C=39
1220 FORA=12328TO13248STEP40:GOSUB1250:NEXTA
1225 RETURN
1250 POKEA,B:POKEA+C,B:POKEA+W,113:POKEA+C+W,113:RETURN

rem for unexpanded Aquarius

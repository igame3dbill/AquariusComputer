new

10 F=13208:H=0
50 GOSUB1000:GOSUB900:K=3:L=1
55 C=INT(6*RND(1)+1):POKEF+L,C+48
60 POKE14337,121:POKE14338,48
63 ?"Now On";L;TAB(14)"Target";I;TAB(28)"Lives";K:GOSUB1080
65 FORA=1TOL:D=7ANDPEEK(F+A):GOSUB700:FORX=1TO300:NEXT:NEXT:A=1
75 GOSUB800:IFD<1ORD>6THENSOUND(40,20):GOTO75
80 GOSUB700:IFD<>(7ANDPEEK(F+A))THENGOSUB850:K=K-1:IFK>0THEN60
85 IFK<1THEN500
90 IFA<>ITHEN95
REM A=I means win condition. Fireworks here
91 FORQ=1TO3:FORX=100TO2STEP-1:SOUND(1,X):NEXT:NEXT
92 FORX=1TO2:FORD=1TO6:GOSUB700:NEXT:NEXT:GOTO500
95 IFA<LTHENA=A+1:GOTO75
100 IFA=LTHENA=A+1:L=L+1:GOTO55
150 END
500 IFK<>0THEN505
501 GOSUB850:FORA=1TOL:D=7ANDPEEK(F+A):GOSUB700:FORX=1TO300:NEXT:NEXT
505 IFK=0THENL=L-1
510 GOTO50
REM play tone and highlight colored block
700 IFD=1THENPOKE13721,7:SOUND(118,68):POKE13721,113
710 IFD=2THENPOKE13732,7:SOUND(99,81):POKE13732,116
720 IFD=3THENPOKE13743,7:SOUND(82,98):POKE13743,114
730 IFD=4THENPOKE14081,7:SOUND(65,123):POKE14081,115
740 IFD=5THENPOKE14092,7:SOUND(54,147):POKE14092,122
750 IFD=6THENPOKE14103,7:SOUND(43,187):POKE14103,120
760 RETURN
800 E$=INKEY$:IFE$<>""THEN800
810 E$=INKEY$:C=RND(1):IFE$=""THEN810
820 D=VAL(E$):RETURN
850 FORX=10TO300:SOUND(2,X):NEXT:RETURN
900 ?CHR$(11):FORX=13312TO13471:POKEX,112:NEXT
901 FORX=13472TO14311:POKEX,0:NEXT
REM uncomment line 902 to cheat
902 REM FORX=14232TO14311:POKEX,6*16:NEXT
905 L=49:A=13598:C=113:GOSUB950:C=116:GOSUB950:C=114
906 GOSUB950:A=13958
910 C=115:GOSUB950:C=122:GOSUB950:C=120:?
950 FORY=ATOA+240STEP40:FORX=0TO6:POKEX+Y,C:NEXT:NEXT:POKEA-901,L
955 A=A+11:L=L+1:RETURN
1000 ?CHR$(11):?TAB(4)"SYMON   ";CHR$(5);" Digital Output 1984":?:?
1010 ?TAB(4)"See packaging for instructions":?:?
1020 ?:?:?TAB(12)"Best Score";H:?TAB(12)"Last Score";L:?:?
1021 IFL>HTHENL=H:?TAB(9)"This is a New Best !":?
1022 INPUT"Play to how many (2-78)";I:IFI<2ORI>78THEN1022
REM 14345 holds cart scrambler - randomize
1023 FORQ=0TO(I+PEEK(14345)):C=RND(1):NEXT:RETURN
1080 forq=1to1250:next:return

REM partial transfer, attempted fix
REM uncomment line 902 to cheat
REM for unexpanded Aquarius

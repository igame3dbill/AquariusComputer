5 REM:by dancresp at zonadepruebas.com
10 GOSUB60
14 A$=INKEY$:IFA$=""THEN34
15 C=13:G=32:P=Y*40+X:GOSUB98
16 IFA$="q"AND(Y>2OR(Y=2AND(X=19ORX=20)))THENY=Y-1
17 IFA$="a"ANDY<22THENY=Y+1
18 IFA$="o"ANDX>1ANDY<23THENX=X-1
19 IFA$="p"ANDX<38ANDY<23THENX=X+1
20 P=Y*40+X:F=12288+P:IFPEEK(F)=14THEN40
22 G=19:C=178:GOSUB98
24 IFY=1THEN50
26 M=ABS((PEEK(F-1)=14)+(PEEK(F+1)=14)+(PEEK(F-40)=14)+(PEEK(F+40)=14))
28 P=2:IFM<>0THENT$=STR$(M)+" MINES  ":GOSUB99:SOUND(20,100)
30 IFM=0THENT$="<<<SAFE>>>":GOSUB99:SOUND(10,50)
32 IFT>YTHENT=Y:S=S+5:P=34:T$=STR$(S):GOSUB99
34 C=-C:IFC=1ANDB>0THENB=B-5
36 GOTO14
40 G=209:C=6:GOSUB98:P=2:T$="GAME OVER   ":GOSUB99
41 FORF=30TO50:SOUND(9,F):NEXT
42 FORF=13432TO14192
43 IFPEEK(F)=102THENPOKEF,6
44 NEXT
46 IFINKEY$=""THEN46
48 GOTO10
50 P=2:T$="YOU DID IT!":GOSUB99
52 FORF=100TO0STEP-20:SOUND(20,F):NEXT
54 FORF=1TO3000:NEXT
56 S=S+B:IFL<160THENL=L+20
58 GOSUB80:GOTO14
60 GOSUB97:P=15:T$="MINED-OUT":GOSUB99
62 PRINT:PRINT"Your mission is to rescue Bill the"
64 PRINT"Worm by breaking down the door of his"
66 PRINT"enclosure which is on then final"
68 PRINT"minefield.":PRINT:PRINT"You begin each level at the bottom of"
70 PRINT"the screen.":PRINT:PRINT"Move using the 'QAOP' keys."
72 PRINTTAB(202);"PRESS ANY KEY"
74 PRINTTAB(198);"(C)Scainet Soft, 2012"
76 IFINKEY$=""THEN76
78 S=0:L=0
80 GOSUB97:C=126:G=216:FORF=40TO79:P=F:GOSUB98:P=F+880:GOSUB98:NEXT
81 FORF=80TO880STEP40:P=F:GOSUB98:P=F+39:GOSUB98:NEXT
82 P=28:T$="SCORE:"+STR$(S):GOSUB99
84 P=2:T$="MINE LAYING...":GOSUB99:P=59:T$="  ":GOSUB99:P=939:GOSUB99
86 C=102:G=14:POKE12369,32
88 FORF=1TO50+L
90 P=INT(RND(1)*760)+120:IFPEEK(12288+P)<>32THEN90
92 GOSUB98
93 NEXT
94 P=940:G=19:C=178:GOSUB98
95 X=20:Y=23:T=Y:B=1000+L*100:C=1
96 P=2:T$="GO...         ":GOSUB99:RETURN
97 PRINTCHR$(11):RETURN
98 POKE13312+P,C:POKE12288+P,G:RETURN
99 I=12288+P:FORF=1TOLEN(T$):POKEI,ASC(MID$(T$,F,1)):I=I+1:NEXT:RETURN
